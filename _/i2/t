-1 23 -45 -0 0N 0W -0W / ~ / ~ / -1 23 -45 0 0N 0W -0W / 
0 1 -1 0n 0w -0w / ~ / ~ / 0.0 1.0 -1.0 0n 0w -0w / 
`abcdefgh``a012`0`12 / `abcdefgh``a012`0`12 / = / =
`"a+b"`"cd"`"*" / `"a+b"`cd`"*" / = / =
"a\0b\t\r\n\"\\" / 0x610062090d0a225c / ~ / "a\0b\t\r\n\"\\" / nyi \0b
(0x;0x00094142;0x01094142) / ("";0x00094142;0x01094142) / ~ / ("";"\0\tAB";0x01094142) / nyi 0x
(\) / \ / \ / \
(') / ' / = / =
(1-2;1--2;- 1) / -1 3 -1 / = / =
(-"A";-"BC";-"") / (-65;-66 -67;!0) / = / =
1-"A" / -64 / = / =
"A"-1 / 64 / = / =
1 2 3+4 5 6 / 5 7 9 / = / =
1 -2 3+-4 / -3 -6 -1 / = / =
(1;2 3)+(4 5;6) / (5 6;8 9) / = / =
(7+();()+8) / (0#,!0;0#,!0) / (();()) / (();())
2+--3*- -4 5*6 / 74 -88 / = / =
1+(2 3;4;(5 6;7 8;9)) / (3 4;5;(6 7;8 9;10)) / = / =
1 2+(3;4 5) / (4;6 7) / = / =
(1;2 3)+4 / (5;6 7) / = / =
(1;2 3)+4 5 / (5;7 8) / = / =
(1 2;3;4 5)+(6;7 8 9;10 11) / (7 8;10 11 12;14 16) / = / =
0 1+0xff / 255 256 / = / =
1 2 3%1 / 1 2 3f / = / 1 2 3
-1 0 1%0 / -∞ ø ∞ / -0w 0n 0w / -0W 0N 0W / ngn i%i does not promote to f
!:'5 0 -1 -3 / ~ / (0 1 2 3 4;!0;,,1;(1 0 0;0 1 0;0 0 1)) / (0 1 2 3 4;!0;,-1;-3 -2 -1)
!!0 / ~ / () / () / k7 length
!2 0 5 / ~ / ~ / (!0;!0;!0) / k7 domain
!3 2 / (0 0 1 1 2 2;0 1 0 1 0 1) / = / = 
(1+!0;(!0)+2) / (!0;!0) / = / =
3 3 17 0!'10 -1 23 1 / (3!10;3!-1;17!23;0!1) / (3!10;3!-1;17!23;0!1) / 1 2 6 0N / ngn needs no colon for !:'
+(1 2 3;4 5 6) / (1 4;2 5;3 6) / = / =
=,1 / 1!!1 / 1!,0 / (,1)!,,0

+(`a;`b`c) / (`a`b;``c) / = / (`a`b;`a`c)
+("abc";"d";"efg")            / ("ade";"bdf";"cdg")
+(("ab";"cd");"e")            / (("ab";"e");("cd";"e"))
+("ab";0 1)                   / (("a";0);("b";1))
+(("ab",0;("c";+;2)))         / ("ac";("b";+);0 2)
++(("ab",0;("c";+;2)))        / (("a";"b";0);("c";+;2))
(+0;+1 2;+(1;`a;"a"))         / (,,0;,1 2;,(1;`a;"a"))
(+;+:;1+;+-;+:-)              / (+;+:;1+;+:-;+:-)
(+/1 2 3;-/4 5;*/0)           / 6 -1 0
(+/1 2 3.5;-/4 5.;*/0.)       / 6.5 -1.0 0.0
*/(2 3;4 5)                   / 8 15
(+/!0;+/0#0n;+/"";+/())       / (0;0.0;0;0)
(-/!0;-/0#0n;-/"";-/())       / (0;0.0;0;0)
(*/!0;*/0#0n;*/"";*/())       / (1;1.0;1;1)
(&/!0;&/0#0n;&/"";&/())       / (0W;0w;0W;0W)
(|/!0;|/0#0n;|/"";|/())       / (-0W;-0w;-0W;-0W)
10+/2 3 4                     / 19
10+\2 3 4                     / 12 15 19
2.+/3 4                       / 9.0
2+/3. 4.                      / 9.0
(*/0#,();*/0#,2 3)            / 1 1
&/0#,(1 2;"34")               / 0W
"x"/("ab";"c";"def")          / "abxcxdef"
"xy"/("ab";"cd")              / "abxycd"
"AB"/"ab"\"abracadabra"       / "ABracadABra"
""/("ab";"cd")                / "abcd"
0xff/,"ab"                    / "ab"
"x"/()                        / ""
3(2*)/1                       / 8
*/`a`b`c!2 3 4                / 24
0{y}/!3                       / 2
{$[x=1;1;2!x;1+3*x;x%2]}/17   / 1
{$[x=1;1;2!x;1+3*x;x%2]}\17   / 17 52 26 13 40 20 10 5 16 8 4 2 1
(+\2 3;-\4 5;1+\2 3;*\!0)     / (2 5;4 -1;3 6;!0)
-\(1 2;3)                     / (1 2;-2 -1)
,\0 1 2                       / (,0;0 1;0 1 2)
123*\!0                       / !0
+\(1 2;3 4;5 6)               / (1 2;4 6;9 12)
{(x;y;z)}/[1;2 3 4;5 6 7]     / ((1 2 5;3;6);4;7)
{(x;y;z)}\[1;2 3 4;5 6 7]     / (1 2 5;(1 2 5;3;6);((1 2 5;3;6);4;7))
*\`a`b`c!2 3 4                / `a`b`c!2 6 24
5*\`a`b`c!2 3 4               / `a`b`c!10 30 120
","\",a,bc,,de,"              / ("";,"a";"bc";"";"de";"")
"a"\"bc"                      / ,"bc"
"x"\""                        / ,""
"ab"\"abcdeabfabab"           / ("";"cde";,"f";"";"")
"\n"\"ab\ncd\n"               / ("ab";"cd")
5(2*)\1                       / 1 2 4 8 16 32
(20>)(2*)/1                   / 32
(20>)(2*)\1                   / 1 2 4 8 16 32
2/1 1 1 1 0 1 1               / 123
2/:1 1 1 1 0 1 1              / 123
3 5 2/2 4 0                   / 28
2/(1 1 0;0 1 0;1 0 1)         / 5 6 1
(8#2)\123                     / 0 1 1 1 1 0 1 1
(1+!8)\123                    / 0 0 0 0 0 2 1 3
(4#3)\16 17                   / (0 0;1 1;2 2;1 2)
2\7 8 9                       / (0 1 1;1 0 0;1 0 0;1 0 1)
2\:7 8 9                      / (0 1 1;1 0 0;1 0 0;1 0 1)
5\!0                          / ()
10\0                          / !0
#:'(1;2 3 4;!0;5 6)           / 1 3 0 2
(!3)#'`a                      / (0#`;,`a;`a`a)
2 0N#"abcdef"                 / ("abc";"def")
0N 2#"abcdef"                 / ("ab";"cd";"ef")
3 0N#"abcdefgh"               / ("ab";"cde";"fgh")
0N 3#"abcdefgh"               / ("abc";"def";"gh")
3 0N#`a`b`c`d`e`f`g           / (,`a;`b`c`d;`e`f`g)
0N 3#`a`b`c`d`e`f`g           / (`a`b`c;`d`e`f;,`g)
0#(1;"b")                     / ()
{0=3!x}#!10                   / 0 3 6 9
{2!x}#12 4 -10                / !0
{x-"a"}#"abcd"                / "bccddd"
{(</)#x}@+!3 4                / (0 1;0 2;0 3;1 2;1 3;2 3)
-':2 3 8 99                   / 2 1 5 91
-':2.5 -5.0 0w                / 2.5 -7.5 0w
*':7 2                        / 7 14
=':"abbr"                     / 0 0 1 0
#:'`a`b!("abr";"acada")       / `a`b!3 5
1;2                           / 2
#1 2 3                        / 3
#4                            / 1
#(1 2;3)                      / 2
#,1 2                         / 1
#""                           / 0
#()                           / 0
#`a`b`c!(!4;4#`;"abcd")       / 3
#+`a`b`c!(!4;4#`;"abcd")      / 4
#(+)                          / 1
8#"abc"                       / "abcabcab"
2#5 6 7                       / 5 6
4#(1 2;"cd";`e`f`g)           / (1 2;"cd";`e`f`g;1 2)
1#!0                          / ,0N
2#""                          / "  "
3#0#`                         / ```
(`a`b!(0 1;"ab");0)@2         / `a`b!(0N 0N;"  ")
0#!0                          / !0
0#"abc"                       / ""
3#(+)                         / (+;+;+)
(!0)#"abc"                    / "a"
0#`                           / 0#`
0#()                          / ()
0 0#0                         / ()
0 2#0                         / ()
-1#"abc"                      / ,"c"
-1#()                         / ,()
-1#(();())                    / ,()
2 3#4 5                       / (4 5 4;5 4 5)
1 2 3#`a`b`c`d                / ,(`a`b`c;`d`a`b)
2 3#("ab";"cd")               / (("ab";"cd";"ab");("cd";"ab";"cd"))
,1                            / ,1
,1 2                          / ,1 2
((),0),1                      / 0 1
*()                           / ()
*"abc"                        / "a"
*""                           / " "
***(,(((1 2;3);4)))           / 1 2
*3#,"ab"                      / "ab"
*`a`b`c!1 2 3                 / 1
`a`b!1 2                      / `a`b!1 2
`a`b!1                        / `a`b!1 1
`a!1 2                        / `a`a!1 2
`a!1                          / (,`a)!,1
!`a`b!1 2                     / `a`b
.`a`b!1 2                     / 1 2
"abc"!"d"                     / "abc"!"ddd"
!{x!x}`a`b                    / `a`b
.{x!x}`a`b                    / `a`b
|`a`b`c!(0 1 2;3;4)           / `c`b`a!(4;3;0 1 2)
|+`a`b`c!(0 1;2 3;4 5)        / +`a`b`c!(1 0;3 2;5 4)
(+`a`b`c!(0 1;2 3;4 5))1      / `a`b`c!1 3 5
 a:2;a:a+3;a+4                / 9
 a:!4;b:3;c:a*b;c             / 0 3 6 9
{2;3 4}5 6 7                  / 3 4
{}0                           /
 a:2;{a}0                     / 2
 a:2;{a:3;a}0                 / 3
 f:{a};a:2;f[]                / 2
 a.b.c:1;a.b.c                / 1
 a.b.c:1;{a.b.c}0             / 1
 a.b.c:1;{a.b.c:2;a.b.c}0     / 2
 {a.b.c:1}0;a.b.c             / 1
 {a.b.c:1}0;a.b.c:2;a.b.c     / 2
 a:`a`b!(0 1;2 3);a.a:a;a     / `a`b!(`a`b!(0 1;2 3);2 3)
{x*x}2 3                      / 4 9
{x+y*z}[1;2;3]                / 7
{x+y*z}[1][2;3]               / 7
{x[y;z]}[+;2;3]               / 5
_("aBC123";"")                / ("abc123";"")
_"X"                          / "x"
_1.2 3.0 -4.5 -6.0            / 1 3 -5 -6
$[1;2;3]                      / 2
$[0;2;3]                      / 3
$[0;4;();5;;6;7]              / 7
$[0;4;();5;;6]                /
$[;`y;`n]                     / `n
 b:1;$[b;b+2;b*5]             / 3
 c:0;$[c;c+2;c*5]             / 0
$("a";"ab";1;2 34;`sym)       / (,"a";(,"a";,"b");,"1";(,"2";"34");"sym")
$(+;-*;%/;1+;\)               / (,"+";"-:*";"%/";"1+";,"\\")
8:                            / 8:
9::                           / 9::
 n:#1:"t.k";(0<n)&n<10000000  / 1
 a:0 0 0 0 0 0 0 0 0;a        / 0 0 0 0 0 0 0 0 0
0x7f<0x8080                   / 1 1
(1+)2                         / 3
((1;2 3)+)(4;5 6)             / (5;7 9)
|!0                           / !0
|()                           / ()
|1                            / 1
|"ab"                         / "ba"
|`a`b`c                       / `c`b`a
|!5                           / 4 3 2 1 0
|(1;2 3)                      / (2 3;1)
0~0                           / 1
1~2                           / 0
 a~a:(3 0N 0W -0W;``a;"")     / 1
()~()                         / 1
(0#`)~(0#`a`b)                / 1
()~0#,""                      / 1
""~()                         / 0
2~,2                          / 0
(0;1;0 1 1 0 0 1;0#1 1)       / (0;1;0 1 1 0 0 1;!0)
0,1                           / 0 1
2,""                          / ,2
0 1 2,3 4                     / 0 1 2 3 4
`a,0                          / (`a;0)
(!0),`a`b                     / `a`b
1 0 1,""                      / 1 0 1
"ab","c"                      / "abc"
"a",""                        / ,"a"
"","a"                        / ,"a"
,/!:'!4                       / 0 0 1 0 1 2
,/((1 2;3);(4;5 6))           / (1 2;3;4;5 6)
,//(,0;((1 2;,,3);"";4 5);6)  / 0 1 2 3 4 5 6
(0#,("ab";3)),0#,`a`b         / ()
&:'(4;0 2 0 0 1;0 0;1)        / (0 0 0 0;1 1 4;!0;,0)
&`a`b`c!2 0 1                 / `a`a`c
&"\t"                         / 0 0 0 0 0 0 0 0 0
"abc"1                        / "b"
"abc"1 1 0                    / "bba"
`a`b`c[1 5 1 0 -10]           / `b``b`a`
1+\:2 3                       / 3 4
1 2+/:3 4 5                   / (4 5;5 6;6 7)
"abc"*\:0 1                   / (0 97;0 98;0 99)
3#/:2 1                       / (2 2 2;1 1 1)
3 2#\:1                       / (1 1 1;1 1)
"abc"@1 4                     / "b "
{1+#x}@/:(4;5 6)              / 2 3
(-:;#:)@'2                    / -2 1
"abcd"@(3 1;(1 7;2))          / ("db";("b ";"c"))
"ab"@!0                       / ""
("ab";`cd;5 6)@""             / ()
(`a;"bc")1                    / "bc"
(1_"abc";-2_`a`b`c;2_3 4)     / ("bc";,`a;!0)
(6_!5;-7_!0)                  / (!0;!0)
-7_((1 2;3);4 5)              / ()
(2 3_"abcde";(!0)_"abcde")    / ((,"c";"de");())
(!0)_`a`b`c                   / ()
2 4 6_`a`b`c`d`e`f            / (`c`d;`e`f;0#`)
1 2 4_0 1 0 1 0 1 0 1 0 1 1 0 / (,1;0 1;0 1 0 1 0 1 1 0)
2 2_0 1                       / (!0;!0)
1_(`a;"b";"c")                / "bc"
"abcd"_2                      / "abd"
(,12)_0                       / !0
("a";1;"c")_1                 / "ac"
(`a;1;"c")_1                  / (`a;"c")
(`a;1;"c")_3                  / (`a;1;"c")
(`a;1;"c")_-1                 / (`a;1;"c")
(,,"")_0                      / ()
`b_`a`b`c!3 4 5               / `a`c!3 5
0 0 1 1&0 1 0 1               / 0 0 0 1
0 0 1 1|0 1 0 1               / 0 1 1 1
~"a b\0c0"                    / 0 0 0 1 0 0
~0 1 1 0 0                    / 1 0 0 1 1
~(`;1 0 2;"")                 / (1;0 1 0;!0)
~0x00                         / 1
~"ab"!"c\0"                   / "ab"!0 1
^(1 2 0W -0W 0N -1;" a";`;()) / (0 0 0 0 1 0;1 0;1;())
()^"ab"                       / ()
(1 2;3)^3                     / ,1 2
"abc"^1 2                     / "abc"
("a";"bc";4;`e)^("b";4)       / ("a";"bc";`e)
"abracadabra"^"ba"            / "rcdr"
`a`b`c^`c`d                   / `a`b
3 4 5^5 5                     / 3 4
2^`a`b`c!(0 1;0N 3;0N)        / `a`b`c!(0 1;2 3;2)
?(1;`a;"aa";`a;`a;1)          / (1;`a;"aa")
?()                           / ()
?"abracadabra"                / "abrcd"
?3 1 4 1 5 9 2 6 5 3 5 8 9    / 3 1 4 5 9 2 6 8
?`a```aa`a`aa`aa```           / `a``aa
3?""                          / "   "
~|/{(x<"a")&"z"<x}100?"a"     / 1
~|/{(x<"A")&"Z"<x}100?"A"     / 1
+/-5?5                        / 10
{x@<x}0N?"abracadabra"        / "aaaaabbcdrr"
"abc"?"abracadabra"           / 0 1 0N 0 2 0 0N 0 1 0N 0
"abc"?"b"                     / 1
("ab";"cd")?("cd";"efg";"h")  / 1 0N 0N
`a`b`c?`b`z                   / 1 0N
`a`b`c?(`b`z;,`c;0#`;())      / (1 0N;,2;!0;())
("ab";("a";1);2;`;"a")?"a"    / 4
(`a`b`c!3 5 8)?5 6            / `b`
(`a`b`c!2 3 4)`b              / 3
(`a`b`c!2 3 4)`c`c`a          / 4 4 2
("abc"!2 3 4)("b";"acz";"")   / (3;2 4 0N;!0)
`c$(65 66 67;"de")            / ("ABC";"de")
`c$("";234;())                / ("";0xea;())
`i$("";0xff;"AB";2345;())     / (!0;255;65 66;2345;())
`c$"A",66                     / "AB"
@[3 4 5 6;1 3;:;8 9]          / 3 8 5 9
@[3 4 5 6;1;:;8 9]            / (3;8 9;5;6)
@[!6;(1 3;1 4);:;(6 7;8 9)]   / 0 8 2 7 9 5
@[("a";`b;3);1;:;("de";`f)]   / ("a";("de";`f);3)
@[();!0;:;0]                  / ()
@[8 0 0 9;1 0 1;:;5 2 4]      / 2 4 0 9
@[`a`b`c`d;();:;"e"]          / `a`b`c`d
@[`a`b!1 2;`b`c;:;3 4]        / `a`b`c!1 3 4
@[(0#`)!();`a;:;1]            / (,`a)!,1
@[(0#`)!();`a;:;1 2]          / (,`a)!,1 2
@["abcd";(0;1 2);:;"e"]       / "eeed"
@["ab";,,1;:;"c"]             / "ac"
@[3 4 5;1;+;1]                / 3 5 5
@[3 4 5;1 1 0;-;1]            / 2 2 5
@[`a`b!3 4;`b`c;*;2]          / `a`b`c!3 8 2
@[(1;2;`);0 1;#;3]            / (,3;3 3;`)
@["ABC";1;_:]                 / "AbC"
="abracadabra"                / "abrcd"!(0 3 5 7 10;1 8;2 9;,4;,6)
=1 2                          / 1 2!(,0;,1)
=""                           / ""!()
=`a`b`c`d!5 7 6 7             / 5 7 6!(,`a;`b`d;,`c)
=:'3 1 0                      / ((1 0 0;0 1 0;0 0 1);,,1;())
(."1+2";."3";.".\"3*4\"";."") / (3;3;12;::)
{x@=x}"abracadabra"           / "abrcd"!("aaaaa";"bb";"rr";,"c";,"d")
{x@<x}"abracadabra"           / "aaaaabbcdrr"
{x@>x}4 1 0 3 2               / 4 3 2 1 0
 a:="abracadabra";#:'a        / "abrcd"!5 2 2 1 1
`r`b#=`a`b`r`a`c`a`d`a`b`r`a  / `r`b!(2 9;1 8)
>#:'="mississippi"            / "ispm"
<"aaabc"                      / 0 1 2 3 4
<(-;+)                        / 1 0
{x@<x}`f`a``c`de`c`b`ghi      / ``a`b`c`c`de`f`ghi
+/<':{x@<x}100?0              / 0
+/>':{x@>x}100?0              / 0
$-123                         / "-123"
$(0W -0W;0N -12345678;9)      / (("0W";"-0W");("0N";"-12345678");,"9")
$(+;-:;0:;9::;`a`bc`)         / (,"+";"-:";"0:";"9::";(,"a";"bc";""))
("abc";"def")[1;2 0]          / "fd"
(**|:)("ab";"cd")             / "c"
(("ab";`c);("ef";7)). 1 0 1   / "f"
("ab";"cd"). 1 1              / "d"
(-).(5;3 1)                   / 2 4
0 1 .()                       / 0 1
`j?"123"                      / 123
`j?"[1,-23,[4],[[[5]],6]]"    / (1;-23;,4;(,,5;6))
`j?"[true,false,null,[]]"     / (1;0;0N;())
`j?"{\"k\":\"v\",\"k1\":2}"   / `k`k1!(,"v";2)
 {a::1}2;a                    / 1
 a:1;{a::2}3;a                / 2
 a:1;{a:2}3;a                 / 1
 a:1;{a:2;a+:3}4;a            / 1
 a:1;{a+:2}3;a                / 3
 a:1;{a:2;a+:3;a}4            / 5
 a:2;a+:3;a                   / 5
{a:2;a+:3;a}4                 / 5
_%1000 100,!10                / 31 10 0 1 1 1 2 2 2 2 2 3
3'!6                          / (0 1 2;1 2 3;2 3 4;3 4 5)
6'!3                          / ()
2'"abcde"                     / ("ab";"bc";"cd";"de")
4'(`;1;-;("ab";"c");!0)       / ((`;1;-;("ab";"c"));(1;-;("ab";"c");!0))
2'("a";"b";3;4)               / ("ab";("b";3);3 4)
{[a;bc;def]a+bc*def}[3;4;5 6] / 23 27
{[n] 1+!n}[3]                 / 1 2 3
("ab";"cd")[;1]               / "bd"
.[("ab";"cd");1 0;,;"e"]      / ("ab";("ce";"d"))
.[1 2;();+;3]                 / 4 5
(`a`b!(1;`c`d!3 4)). `b`c     / 3
(`a!,`b!,`c!3).`a`b`c         / 3
 a:``b!(`;``c!(`;12));.`a`b`c / 12
 a:``b!(`;``c!(`;12));a.b.c   / 12
()                            / ()
(`a`b!1 2)+3                  / `a`b!4 5
2*("ab"!3 4)                  / "ab"!6 8
("abc"!1 2 3)-"bda"!4 5 6     / "abcd"!-5 -2 3 -5
{f:1+2*;f 3}0                 / 7
 f:-;f[1;2]                   / -1
{x+x}2                        / 4
{[n]n*n}3                     / 9
&{x!x}@!4                     / 1 2 2 3 3 3
1+{x!x}@!2                    / 0 1!1 2
-4!123                        / 30
 a:3;1+a*:2                   / 7
.()                           / ()
.(+;1;2)                      / 3
.(-:;3)                       / -3
$`:k.com:80`:/bin/ls`:1.2.3.4 / (":k.com:80";":/bin/ls";":1.2.3.4")
.:'(+;+:;';++;+/)             / (1;1;0;(+:;+);+)
{s:"abcd";s[2]:"x";s}0        / "abxd"
 s:"abcd";s[2]:"x";s          / "abxd"
 a:1 2;.[`a;1;+;3]            / 1 5
1.2<2                         / 1
3.4<1.2 3.4 5.6               / 0 0 1
1.0+2 3                       / 3.0 4.0
,1.5                          / ,1.5
,2.5                          / ,2.5
 f:"abracadabra"1+;(@f)~@(++) / 1
 f:"abracadabra"1+;f 3        / "c"
{x[0]-1}3 4 5                 / 2
 a+:a:!2;a                    / 0 2
-`a`b`c`d!2 -3 4 0            / `a`b`c`d!-2 3 -4 0
4$"xy"                        / "xy  "
 a:0;(a;a:1;a)                / 1 1 0
("abc";"def";"ghi")[1  ;2  ]  / "f"
("abc";"def";"ghi")[1  ;2 0]  / "fd"
("abc";"def";"ghi")[1 2;2  ]  / "fi"
("abc";"def";"ghi")[1 2;2 0]  / ("fd";"ig")
(1+)2                         / 3
+[1]2                         / 3
+[1;]2                        / 3
#[1 2;]                       / 1 2#
#'[1 2;]                      / 1 2#'
2+                            / 2+
+[2]                          / +[2]
+[2;]                         / 2+
1 2#'                         / 1 2#'
1 2+/                         / 1 2+/
{f:+';f[1 2]}0                / +'[1 2]
(`a`b!0 1;`a`b!2 3)           / +`a`b!(0 2;1 3)
((+;-)!((*;%)!2 3;4))[+;%]    / 3
,(::)                         / ,(::)
{-x,:x}@,!2                   / (0 -1;0 -1)
(1;,2)=3.0                    / (0;,0)
`k("";();0x410100;"abc")      / "(\"\";();0x410100;\"abc\")"
`k(0#,`;`a``bc`:dir/file.ext) / "(0#`;`a``bc`:dir/file.ext)"
`k(+;-:;+/;++;1+;\;':;{y-1})  / "(+;-:;+/;+:+;1+;\\;':;{y-1})"
`k(2:;9::;{};::)              / "(2:;9::;{};::)"
{(a;b):2 3;a-b}0              / -1
 (a;b):3 4;{(c;d):!2;d+b}0    / 5
(+`a`b!(0 1;2 3))1            / `a`b!1 3
(+`a`b!(0 1;2 3))2            / `a`b!0N 0N
(+`a`b!(0 1;2 3))`b           / 2 3
,`a`b!0 1                     / +`a`b!(,0;,1)
,`a`b!(0 1;2)                 / +`a`b!(,0 1;,2)
,`a`b!(0 1;2 3)               / +`a`b!(,0 1;,2 3)
`a!`b!1                       / (,`a)!+(,`b)!,,1
,`a!`b!1                      / +(,`a)!,+(,`b)!,,1
`a!`b!`c!1                    / (,`a)!+(,`b)!,+(,`c)!,,1
.[-:;`;`e]                    / `e
.[+;``;`e]                    / `e
.[.:;,")";`e]                 / `e
.[.:;,"(";`e]                 / `e
.[.:;,"(]";`e]                / `e
.[!:;`;`e]                    / `e
.[?;(-9;!8);`e]               / `e
.[?;(-9;!8);`e]               / `e
.[" "\;1;`e]                  / `e
.[" "/;1;`e]                  / `e
.[2 -3#;4;`e]                 / `e
.[<;(+;-);`e]                 / `e
